buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
    }
}


plugins {
    id 'org.jetbrains.kotlin.multiplatform' version '1.3.60' // or any other kotlin plugin
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.3.60'
}

apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'

repositories {
    jcenter()
    maven { url "https://dl.bintray.com/kotlin/ktor" }
    maven { url 'https://dl.bintray.com/subroh0508/maven' }
    maven { url "http://dl.bintray.com/kotlin/kotlinx.html" }
    maven { url 'http://dl.bintray.com/kotlin/kotlin-js-wrappers' }
    mavenCentral()
}
def ktor_version = '1.2.6'
def logback_version = '1.2.3'

kotlin {
    jvm() {
        withJava()
        jvmJar {
            manifest {
                attributes 'Main-Class': 'com.plusmobileapps.blog.ApplicationKt'
            }
            from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
        }
    }

    js {
        browser {
        }
    }
    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }
        jvmMain {
            dependencies {
                implementation kotlin('stdlib-jdk8')
                implementation "io.ktor:ktor-server-netty:$ktor_version"
                implementation "io.ktor:ktor-html-builder:$ktor_version"
                implementation "io.ktor:ktor-freemarker:$ktor_version"
                implementation "io.ktor:ktor-auth:$ktor_version"
                implementation "io.ktor:ktor-locations:$ktor_version"
                implementation "ch.qos.logback:logback-classic:$logback_version"
                implementation "io.ktor:ktor-gson:$ktor_version"
                implementation "com.zaxxer:HikariCP:$hikaricp_version"
                implementation "org.jetbrains.exposed:exposed:$exposed_version"
                implementation "org.postgresql:postgresql:$postgres_version"
                implementation "io.ktor:ktor-auth-jwt:$ktor_version"
            }
        }
        jvmTest {
            dependencies {
                implementation kotlin('test')
                implementation kotlin('test-junit')
            }
        }
        jsMain {
            dependencies {
                implementation kotlin('stdlib-js')
                implementation "subroh0508.net.kotlinmaterialui:core:0.2.6"
                implementation "org.jetbrains.kotlinx:kotlinx-html-js:0.6.12"
                implementation "org.jetbrains:kotlin-react:16.9.0-pre.89-kotlin-1.3.60"
                implementation "org.jetbrains:kotlin-react-dom:16.9.0-pre.89-kotlin-1.3.60"
                implementation "org.jetbrains:kotlin-css-js:1.0.0-pre.89-kotlin-1.3.60"
                implementation "org.jetbrains:kotlin-styled:1.0.0-pre.89-kotlin-1.3.60"
                implementation(npm("react", "16.9.0"))
                implementation(npm("react-dom", "16.9.0"))
                implementation(npm("styled-components", "^4.3.2"))
                implementation(npm("inline-style-prefixer", "^5.1.0"))
                implementation(npm("@material-ui/core", "3.9.2"))
            }
        }
        jsTest {
            dependencies {
                implementation kotlin('test-js')
            }
        }
    }
}

jvmJar {
    dependsOn(jsBrowserWebpack)
    from(new File(jsBrowserWebpack.entry.name, jsBrowserWebpack.outputPath))
}

task stage {
    dependsOn jvmJar
}

task runWebApp(type: JavaExec, dependsOn: [jvmJar]) {
    group = "application"
    main = "com.plusmobileapps.blog.ApplicationKt"
    classpath(configurations.jvmRuntimeClasspath, jvmJar)
    args = []
}
